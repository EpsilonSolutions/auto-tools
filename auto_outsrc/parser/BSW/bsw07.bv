name := bsw07
setting := symmetric

N := 2
secparam := 80

BEGIN :: types
policy_str := str
policy := object
attrs := list{str}
sh := symmap
coeff := list
S := list{str}
M := GT
Dj := list
Djp := list
Cr := list
Cpr := list
END :: types

BEGIN :: func:setup
input := None
g := random(G1)
#g2 := random(G1)
alpha := random(ZR)
beta := random(ZR)

h := g^beta
f := g^(1/beta)
i := g^alpha
egg := e(g, g)^alpha

mk := list{beta, i}
pk := list{g, h, f, egg}

output := list{mk, pk}
END :: func:setup


BEGIN :: func:keygen
input := list{pk, mk, S} 

pk := expand{g, h, f, egg}
mk := expand{beta, i}

r := random(ZR)
p0 := pk#1^r
D := (mk#1 * p0)^(1/mk#0)

Y := len(S)
BEGIN :: for
for{y := 0, Y}
s_y := random(ZR)
y0 := S#y
Dj#y0 := p0 * (H(y0, G1)^s_y)
Djp#y0 := g^s_y
END :: for

sk := list{D, Dj, Djp}
output := sk
END :: func:keygen


BEGIN :: func:encrypt
input := list{pk, M, policy_str}
pk := expand{g, h, f, egg}

policy := createPolicy(policy_str)
attrs := getAttributeList(policy)
s := random(ZR)
sh := calculateSharesDict(s, policy)
Y := len(sh)

Ctl := M * egg^s
C    := h ^ s

BEGIN :: for
for{y := 0, Y}
y1 := attrs#y
Cr#y1 := g ^ sh#y1
Cpr#y1 := (H(y1, G1))^sh#y1
END :: for

ct := list{policy_str, Ctl, C, Cr, Cpr}
output := ct
END :: func:encrypt


BEGIN :: func:decrypt
input := list{pk, sk, S, ct}
ct := expand{policy_str, Ctl, C, Cr, Cpr}
sk := expand{D, Dj, Djp}

policy := createPolicy(policy_str)
attrs  := prune(policy, S)
coeff := getCoefficients(policy)

Y := len(attrs)
A := { prod{y := attrs#1, Y} on ((e(Cr#y, Dj#y) / e(Djp#y, Cpr#y))^coeff#y) }

result0 := e(C, D)
result1 := result0 / A
M := Ctl / result1

output := M
END :: func:decrypt
